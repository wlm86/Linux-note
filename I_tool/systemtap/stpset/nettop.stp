#! /usr/bin/env stap
# get nic's recieve and transmit count
# PID — the ID of the listed process.
# UID — user ID. A user ID of 0 refers to the root user.
# DEV — which ethernet device the process used to send / receive data (for example, eth0, eth1)
# XMIT_PK — number of packets transmitted by the process
# RECV_PK — number of packets received by the process
# XMIT_KB — amount of data sent by the process, in kilobytes
# RECV_KB — amount of data received by the service, in kilobytes
# ./net.stp 
#   PID   UID DEV          XMIT_PK RECV_PK XMIT_KB RECV_KB COMMAND        
#   16798     0 eth0               5       0       0       0 ping           
#   13439     0 eth0               5       0       0       0 sshd           
#
#   PID   UID DEV          XMIT_PK RECV_PK XMIT_KB RECV_KB COMMAND        
#   13439     0 eth0               6       0       1       0 sshd           
#   16798     0 eth0               5       0       0       0 ping

global ifxmit, ifrecv
global ifmerged

probe netdev.transmit
{
  ifxmit[pid(), dev_name, execname(), uid()] <<< length
  ifmerged[pid(), dev_name, execname(), uid()] <<< 1
}

probe netdev.receive
{
  ifrecv[pid(), dev_name, execname(), uid()] <<< length
  ifmerged[pid(), dev_name, execname(), uid()] <<< 1
}

function print_activity()
{
  printf("%5s %5s %-12s %7s %7s %7s %7s %-15s\n",
         "PID", "UID", "DEV", "XMIT_PK", "RECV_PK",
         "XMIT_KB", "RECV_KB", "COMMAND")

  foreach ([pid, dev, exec, uid] in ifmerged-) {
    n_xmit = @count(ifxmit[pid, dev, exec, uid])
    n_recv = @count(ifrecv[pid, dev, exec, uid])
    printf("%5d %5d %-12s %7d %7d %7d %7d %-15s\n",
           pid, uid, dev, n_xmit, n_recv,
           @sum(ifxmit[pid, dev, exec, uid])/1024,
           @sum(ifrecv[pid, dev, exec, uid])/1024,
           exec)
  }

  print("\n")

  delete ifxmit
  delete ifrecv
  delete ifmerged
}

probe timer.ms(5000), end, error
{
  print_activity()
}
